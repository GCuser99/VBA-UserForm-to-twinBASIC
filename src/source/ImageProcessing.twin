Module ImageProcessing
    Option Explicit

    #If VBA7 Then
    Private Declare PtrSafe Function CryptBinaryToString Lib "crypt32" Alias "CryptBinaryToStringW" (ByVal pbBinary As LongPtr, ByVal cbBinary As Long, ByVal dwFlags As Long, ByVal pszString As LongPtr, pcchString As Long) As Long
    Private Declare PtrSafe Function CryptStringToBinary Lib "crypt32" Alias "CryptStringToBinaryW" (ByVal pszString As LongPtr, ByVal cchString As Long, ByVal dwFlags As Long, ByVal pbBinary As LongPtr, pcbBinary As Long, Optional ByVal pdwSkip As Long, Optional ByVal pdwFlags As Long) As Long
    #Else
    Private Declare Function CryptBinaryToString Lib "crypt32" Alias "CryptBinaryToStringW" (ByVal pbBinary As Long, ByVal cbBinary As Long, ByVal dwFlags As Long, ByVal pszString As Long, pcchString As Long) As Long
    Private Declare Function CryptStringToBinary Lib "crypt32" Alias "CryptStringToBinaryW" (ByVal pszString As Long, ByVal cchString As Long, ByVal dwFlags As Long, ByVal pbBinary As Long, pcbBinary As Long, Optional ByVal pdwSkip As Long, Optional ByVal pdwFlags As Long) As Long
    #End If

    'https://learn.microsoft.com/en-us/windows/win32/api/wincrypt/nf-wincrypt-cryptbinarytostringa
    Const CRYPT_STRING_BASE64HEADER As Long = &H0 'Base64, with certificate beginning and ending headers.
    Const CRYPT_STRING_BASE64 As Long = &H1 'Base64, without headers.
    Const CRYPT_STRING_BINARY As Long = &H2 'Pure binary copy.
    Const CRYPT_STRING_BASE64REQUESTHEADER As Long = &H3 'Base64, with request beginning and ending headers.
    Const CRYPT_STRING_HEX As Long = &H4 'Hexadecimal only.
    Const CRYPT_STRING_HEXASCII As Long = &H5 'Hexadecimal, with ASCII character display.
    Const CRYPT_STRING_BASE64X509CRLHEADER As Long = &H9 'Base64, with X.509 CRL beginning and ending headers.
    Const CRYPT_STRING_HEXADDR As Long = &HA 'Hexadecimal, with address display.
    Const CRYPT_STRING_HEXASCIIADDR As Long = &HB 'Hexadecimal, with ASCII character and address display.
    Const CRYPT_STRING_HEXRAW As Long = &HC 'A raw hexadecimal string. Windows Server 2003 and Windows XP:  This value is not supported.
    Const CRYPT_STRING_STRICT As Long = &H20000000& 'Enforce strict decoding of ASN.1 text formats.
    'the following two can be added to one of the flags above
    Const CRYPT_STRING_NOCRLF As Long = &H40000000& 'Do not append any new line characters to the encoded string.
    Const CRYPT_STRING_NOCR As Long = &H80000000& 'Only use the line feed (LF) character (0x0A) for a new line.
     
    Public Function GetImageString(pic As stdole.IPictureDisp) As String
        Dim bytes() As Byte
        If pic Is Nothing Then Exit Function
        bytes = BytesFromPicture(pic)
        'offset determined through some reverse engineering...
        GetImageString = Mid(ToBase64Array(bytes), 73)
    End Function

    Public Function ToBase64Array(bytes() As Byte) As String
        'https://gist.github.com/wqweto/0002b7e6c4f92e69c8e8339ed2235b4c
        Dim lSize As Long
        If UBound(bytes) >= 0 Then
            ToBase64Array = String$(2 * UBound(bytes) + 6, 0)
            lSize = Len(ToBase64Array) + 1
            Call CryptBinaryToString(VarPtr(bytes(0)), UBound(bytes) + 1, CRYPT_STRING_BASE64 Or CRYPT_STRING_NOCRLF, StrPtr(ToBase64Array), lSize)
            ToBase64Array = Left$(ToBase64Array, lSize)
        End If
    End Function

    Public Function BytesFromPicture(pic As IPictureDisp) As Byte()
        Dim pb As PropertyBag
        Set pb = New PropertyBag
        pb.WriteProperty "Image", pic
        BytesFromPicture = pb.Contents
    End Function
    
    Public Function GetTransparentIconString() As String
    	'used to hide the icon box on the form title bar
        Dim bytes() As Byte
        bytes = LoadResData("transparent.ico", "IMAGES")
        GetTransparentIconString = ToBase64Array(bytes)
    End Function
    
    Public Function GetImageFromResources(ByVal resourceId As Variant, ByVal resourceFolder As Variant) As StdPicture
        Dim bytes() As Byte
        bytes = LoadResData(resourceId, resourceFolder)
        Return Global.LoadPicture(bytes)
    End Function
        
End Module